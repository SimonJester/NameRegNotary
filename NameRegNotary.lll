;; Name Registration with Notary
;;
;; USAGE
;;     If not registered yet, register name; otherwise return timestamp:
;;         (send NameRegNotaryAddr "MyDAppName" valueInWei)
;;     Unregister (only works if sent from original registered address):
;;         (send NameRegNotaryAddr valueInWei)
;;
;; Should be backwards compatible with NameReg -- the only difference is 
;; that it returns a timestamp if you try to register an already registered 
;; name.
;;
;; Derived from NameReg, which was taken from: 
;;     https://github.com/ethereum/cpp-ethereum/wiki/LLL-Examples-for-PoC-5

{
    ;; Initialize
    [thisName]: "NameRegNotary"
    [[(address)]] @thisName
    [[@thisName]] (address)
    [[(sha3 @thisName)]] (timestamp)
    [[69]] (caller)
  
    (return 0 (lll
        ;; The first argument (name) exists
        (if (calldatasize)

            (if @@(calldataload 0)
                ;; First arg is already registered, return timestamp.
                (return @@(sha3 (calldataload 0)))

                ;; Name is not registered, so 
                ;; store sender at name, and name at sender.
                {
                    (when @@(caller) [[@@(caller)]] 0)
                    [[(calldataload 0)]] (caller)
                    [[(caller)]] (calldataload 0)
                    (stop)
                }
            )

            ;; No arguments - either unregister or suicide if from owner.
            {
                ;; Suicide if it's from owner's address.
                (when (= (caller) @@69) (suicide (caller)))
  
                ;; Otherwise, unregister name.
                (when @@(caller) {
                    [[@@(caller)]] 0
                    [[(caller)]] 0
                })
                (stop)
            }

        )
    0))
}
